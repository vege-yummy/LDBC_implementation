PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX onto: <http://www.ontotext.com/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
select distinct ?fr ?first ?last ?message ?content ?date where
{
    {
        select ?fr ?message ?date where {
            {
                select distinct ?fr
                where {
                    {
                        sn:pers%personId% snvoc:directKnows ?fr.
                    } union {
                        sn:pers%personId% snvoc:directKnows/snvoc:directKnows ?fr.
                        filter ( ?fr != sn:pers%personId%)
                    }
                }
            }
            ?message snvoc:hasCreator ?fr.
            ?message snvoc:creationDate ?date.
            filter (?date < "%maxDate%"^^xsd:dateTime).
        }
        order by desc (?date) ?message
        limit 20
    }
    ?fr snvoc:firstName ?first .
    ?fr snvoc:lastName ?last .
    {
        ?message snvoc:content ?content .
    } union {
        ?message snvoc:imageFile ?content .
    }
}