PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX onto: <http://www.ontotext.com/>

SELECT distinct ?repliedComment ?repliedCommentContent ?repliedCommentCreationDate ?replyAuthor ?replyAuthorFirstName
?replyAuthorLastName ?knowsAuthor
where {
	?message snvoc:id "%messageId%"^^xsd:long .
    ?message snvoc:hasCreator ?messageAuthor .
    ?repliedComment snvoc:replyOf ?message .
    ?repliedComment rdf:type snvoc:Comment .
    ?repliedComment snvoc:hasCreator ?replyAuthor .
    ?repliedComment snvoc:content ?repliedCommentContent ;
    				snvoc:creationDate ?repliedCommentCreationDate .
    ?replyAuthor snvoc:firstName ?replyAuthorFirstName ;
                  snvoc:lastName ?replyAuthorLastName .
    bind(if (exists{ ?replyAuthor snvoc:directKnows ?messageAuthor . }, "true", "false") as ?knowsAuthor)
} ORDER BY DESC(?repliedCommentCreationDate) ?replyAuthor
