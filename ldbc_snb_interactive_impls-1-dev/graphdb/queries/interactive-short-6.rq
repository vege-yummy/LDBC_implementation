PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?forum ?forumTitle ?moderator ?moderatorFirstName ?moderatorLastName
WHERE {
    ?message snvoc:id "%messageId%"^^xsd:long .
    OPTIONAL{
       ?message snvoc:replyOf* ?originalPostOfComment .
        ?originalPostOfComment rdf:type snvoc:Post .
    }
    BIND(COALESCE(?originalPostOfComment, ?message) AS ?originalPost)
    ?forum snvoc:containerOf ?originalPost .
    ?forum snvoc:hasModerator ?moderator ;
           snvoc:title ?forumTitle .
    ?moderator snvoc:firstName ?moderatorFirstName;
               snvoc:lastName ?moderatorLastName .
}